open Import

module Identity = struct
  type t =
    | Workspace of Path.Source.t
    | Mounted of
        { package : Package.Name.t
        ; project_root : Path.Local.t
        }

  let workspace root = Workspace root
  let mounted ~package ~project_root = Mounted { package; project_root }

  let equal a b =
    match a, b with
    | Workspace a, Workspace b -> Path.Source.equal a b
    | Mounted a, Mounted b ->
      Package.Name.equal a.package b.package
      && Path.Local.equal a.project_root b.project_root
    | Workspace _, Mounted _ | Mounted _, Workspace _ -> false
  ;;

  let repr =
    Repr.variant
      "loaded-project-identity"
      [ Repr.case "Workspace" Path.Source.repr ~proj:(function
          | Workspace root -> Some root
          | Mounted _ -> None)
      ; Repr.case
          "Mounted"
          Repr.(pair Package.Name.repr Path.Local.repr)
          ~proj:(function
            | Mounted { package; project_root } -> Some (package, project_root)
            | Workspace _ -> None)
      ]
  ;;

  let digest t = Dune_digest.repr repr t

  let to_dyn = function
    | Workspace root -> Dyn.variant "Workspace" [ Path.Source.to_dyn root ]
    | Mounted { package; project_root } ->
      Dyn.variant
        "Mounted"
        [ Dyn.record
            [ "package", Package.Name.to_dyn package
            ; "project_root", Path.Local.to_dyn project_root
            ]
        ]
  ;;
end

module Package_view = struct
  type t =
    | Workspace of Package_scope.t
    | Mounted of
        { package_scope : Package_scope.t
        ; owner_package : Package.t
        ; owner_project : Dune_project.t
        }

  let workspace package_scope = Workspace package_scope

  let mounted ~package_scope ~owner_package ~owner_project =
    Mounted { package_scope; owner_package; owner_project }
  ;;

  let package_scope = function
    | Workspace package_scope | Mounted { package_scope; _ } -> package_scope
  ;;

  let visible_packages = function
    | Workspace _ -> None
    | Mounted { owner_package; _ } ->
      Some (Package.Name.Set.singleton (Package.name owner_package))
  ;;

  let owner = function
    | Workspace _ -> None
    | Mounted { owner_package; owner_project; _ } -> Some (owner_package, owner_project)
  ;;
end

type t =
  { project : Dune_project.t
  ; identity : Identity.t
  ; source_root : Source_path.t
  ; source_tree_root : Source_tree.Rules.Dir.t
  ; partition : Build_partition.t
  ; output_root : Path.Build.t
  ; package_view : Package_view.t
  }

let create
      ~project
      ~identity
      ~source_root
      ~source_tree_root
      ~partition
      ~output_root
      ~package_view
  =
  if
    not
      (Source_path.equal source_root (Source_tree.Rules.Dir.source_path source_tree_root))
  then
    Code_error.raise
      "Loaded project source root and source-tree root disagree"
      [ "source_root", Source_path.to_dyn source_root
      ; "source_tree_root", Source_tree.Rules.Dir.to_dyn source_tree_root
      ];
  if
    not
      (Path.is_descendant
         (Path.build output_root)
         ~of_:(Path.build (Build_partition.output_root partition)))
  then
    Code_error.raise
      "Loaded_project output root is outside its build partition"
      [ "output_root", Path.Build.to_dyn output_root
      ; "partition", Build_partition.to_dyn partition
      ];
  { project
  ; identity
  ; source_root
  ; source_tree_root
  ; partition
  ; output_root
  ; package_view
  }
;;

let project t = t.project
let identity t = t.identity
let source_root t = t.source_root
let source_tree_root t = t.source_tree_root
let partition t = t.partition
let output_root t = t.output_root
let visible_packages t = Package_view.visible_packages t.package_view
let package_scope t = Package_view.package_scope t.package_view
let package_owner t = Package_view.owner t.package_view

let output_path t source_path =
  Source_path.descendant source_path ~of_:t.source_root
  |> Option.map ~f:(Path.Build.append_local t.output_root)
;;

let source_path t output_path =
  Path.drop_prefix (Path.build output_path) ~prefix:(Path.build t.output_root)
  |> Option.map ~f:(Source_path.append_local t.source_root)
;;

let source_tree_dir t output_path =
  match Path.drop_prefix (Path.build output_path) ~prefix:(Path.build t.output_root) with
  | None -> Memo.return None
  | Some local -> Source_tree.Rules.Dir.find_dir t.source_tree_root local
;;

let to_dyn t =
  Dyn.record
    [ "identity", Identity.to_dyn t.identity
    ; "source_root", Source_path.to_dyn t.source_root
    ; "source_tree_root", Source_tree.Rules.Dir.to_dyn t.source_tree_root
    ; "partition", Build_partition.to_dyn t.partition
    ; "output_root", Path.Build.to_dyn t.output_root
    ; "visible_packages", Dyn.option Package.Name.Set.to_dyn (visible_packages t)
    ]
;;
